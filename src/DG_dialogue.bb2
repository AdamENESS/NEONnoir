; =============================================================================
; DG_dialogue
; -----------------------------------------------------------------------------
; Dialogue related statements and functions
; Prefix DG = DialoGue
;

; Displays and runs the dialog at the given id
; =============================================================================
#DG_x_pos = 112
#DG_width = 32
#DG_x_padding = #DG_x_pos + #TX_frame_size
#DG_y_padding = 8
Statement DG_show_dialog{id.w}
  !DB_trace{"DG", "DG_show_dialog:" + Str$(id)}
  SHARED DG_dialogue_is_active, NS_script_queue
  !__use_dialogues{}
  !__use_string_table{}

  DEFTYPE .DG_dialogue *current
  DEFTYPE .DG_page *current_page
  DEFTYPE .b displayed, lmb, page_id
  DEFTYPE .w pointer_id

; We won't have more than 8 choices, right?
  Dim rects.NN_screen_rect(8) 

  DG_dialogue_is_active = True
  *current = !__get_dialogue{id}
  page_id = *current\first_page_id

  displayed = False
  pointer_id = 0

  While DG_dialogue_is_active
    VWait
    DisplaySprite #CP_coplist, pointer_id, MouseX, MouseY, 0
    lmb = IN_update_mouse{}

    ; Get the current page
    *current_page = !__get_page{page_id}

    ; If the page is not enabled, advance until we find one that is.
    Repeat
      If *current_page\enabled = False
        page_id = *current_page\page_id
        displayed = False
      End If
    Until page_id <> -1

    If page_id <> -1
      *current_page = !__get_page{page_id}
      If displayed = False
        UnBuffer 0
        DEFTYPE .TX_context ctx
        TX_begin{&ctx}

        DEFTYPE .l size
        size = TX_put_text{&ctx, *current_page\text_id, #DG_width}

        If *current_page\first_choice_id <> -1
          ctx\y_position + 1
          DEFTYPE .DG_choice *choice
          DEFTYPE .w cwidth, cheight

          For c = 0 To *current_page\choice_count - 1
            rects(c)\x0 = #DG_x_padding
            rects(c)\y0 = (ctx\y_position + 1) * #TX_font_height + #DG_y_padding

            *choice = !__get_choice{*current_page\first_choice_id + c}
            size = TX_put_text{&ctx, *choice\text_id, #DG_width}

            cwidth = !__get_size_width{size}
            cheight = !__get_size_height{size}
            rects(c)\x1 = rects(c)\x0 + (cwidth * #TX_font_width)
            rects(c)\y1 = rects(c)\y0 + (cheight * #TX_font_height)
          Next
        End If

        TX_end{&ctx, #DG_x_pos, #DG_y_padding}
        displayed = True
      End If

      If *current_page\choice_count > 0
        DEFTYPE .w selected
        DEFTYPE .DG_choice *choice
        selected = IN_check_mouse{&rects(0), *current_page\choice_count}

        If selected <> -1
          *choice = !__get_choice{(*current_page\first_choice_id + selected)}
          pointer_id = #NN_pointer_interact - #NN_pointer_start

          If lmb = #IN_mouse_released
            NS_script_queue = *choice\script_offset
            page_id = *choice\page_id
            displayed = False
          End If
        Else
          pointer_id = #NN_pointer_default - #NN_pointer_start
        End If
      Else
        If lmb = #IN_mouse_released
          page_id = *current_page\page_id
          displayed = False
        End If
      End If
    End If

    If page_id = -1 Then DG_dialogue_is_active = False
  Wend

  UnBuffer 0
End Statement

; Enables/Disables a page
; =============================================================================
Statement DG_enable_page{id.w, enabled.b}
  !DB_trace{"DG", "DG_enable_page:" + Str$(id) + ", " + Str$(enabled)}
End Statement

; Enables/Disables a choice
; =============================================================================
Statement DG_enable_choice{id.w, enabled.w}
  !DB_trace{"DG", "DG_enable_choice:" + Str$(id) + ", " + Str$(enabled)}
End Statement

; Changes the speaker's portrait
; =============================================================================
Statement DG_set_speaker_image{image_id.w}
  !DB_trace{"DG", "DG_set_speaker_image:" + Str$(image_id)}
End Statement

; Changes the speaker's name
; =============================================================================
Statement DG_set_speaker_name{name_id.w}
  !DB_trace{"DG", "DG_set_speaker_name:" + Str$(name_id)}
End Statement

; Ends the current dialogue
; =============================================================================
Statement DG_end{}
  !DB_trace{"DG", "DG_end"}
  SHARED DG_dialogue_is_active

  DG_dialogue_is_active = False
End Statement